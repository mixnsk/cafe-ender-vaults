plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.codemc.org/repository/nms/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(':api')
    implementation project(':nms')
    implementation project(':v1_8_R3')
    implementation project(':v1_11_R1')
    implementation project(':v1_14_R1')
    implementation project(':v1_17_R1')
    implementation project(':v1_18_R1')
    implementation project(':v1_19_R2')
    compileOnly 'org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.6.2'
    implementation 'de.tr7zw:item-nbt-api:2.12.0'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': project.ext.version
        duplicatesStrategy = 'include'
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = "EnderVaults-Bukkit-v${project.ext.version}.jar"

    relocate 'de.tr7zw.changeme.nbtapi', 'com.github.dig.endervaults.lib.nbt'
    relocate 'com.zaxxer.hikari', 'com.github.dig.endervaults.lib.hikari'
    relocate 'org.mariadb.jdbc', 'com.github.dig.endervaults.lib.mariadb'
    relocate 'org.javatuples', 'com.github.dig.endervaults.lib.javatuples'
}

build.dependsOn shadowJar